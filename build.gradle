plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.3'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.jackson.learn'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
task installFrontend(type: Exec){
	workingDir 'frontend'
	commandLine 'yarn', 'install'
}
task buildFrontend(type: Exec) {
	dependsOn installFrontend
	workingDir 'frontend'
	commandLine 'yarn', 'build'
}

task cleanStatic(type: Delete) {
	delete 'src/main/resources/static'
}
task copyFrontendAssets(type: Copy) {
	dependsOn buildFrontend
	dependsOn cleanStatic
	from 'frontend/dist'
	into './src/main/resources/static'
}

 processResources {
	dependsOn copyFrontendAssets
}

bootRun {
	dependsOn copyFrontendAssets
}
tasks.named('test') {
	useJUnitPlatform()
}
